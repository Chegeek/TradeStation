[LegacyColorValue = true]; 

{
Function    : sfgf.rdate2gdate
Description : Convert an R.D. date to a Gregorian date
Reference   : Calendrical Calculations, Cambridge Press, 2002

Copyright 2016 Scottfree Gains LLC

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
}

Inputs:
	RDate(Numeric),
	GYear(NumericRef),
	GMonth(NumericRef),
	GDay(NumericRef);

Variables:
	priordays(0),
	correction(0);

sfgf.rdate2gdate = False;
GYear = sfgf.rdate2gyear(RDate);
priordays = RDate - sfgf.gdate2rdate(GYear, 1, 1);
Value1 = sfgf.gdate2rdate(GYear, 3, 1);
If RDate < Value1 Then
	correction = 0
Else If RDate >= Value1 and sfgf.gleapyear(GYear) Then
	correction = 1
Else
	correction = 2;
GMonth = Floor((12 * (priordays + correction) + 373) / 367);
GDay = RDate - sfgf.gdate2rdate(GYear, GMonth, 1) + 1;
sfgf.rdate2gdate = True;
