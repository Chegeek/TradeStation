[LegacyColorValue = true]; 

{*******************************************************************
Name		: splatri.calendar
Description	: Display key dates
Provided By	: SplatR (c) Copyright 2013
********************************************************************}

Variables:
	GYear(0),
	GMonth(0),
	GDay(0),
	RDate(0),
	bdw(0),
	bdws(""),
	FDay(False),
	LDay(False),
	Holidays(0),
	HolidayIndex(0),
	HYear(0),
	HMonth(0),
	HDay(0),
	HDays(0),
	NearRD(0),
	NearName(""),
	ih(0),
	Events(0),
	EventIndex(0),
	EYear(0),
	EMonth(0),
	EDay(0),
	EDays(0),
	ie(0);

Arrays:
	Holiday[20](0),
	HolidayName[20](""),	
	Event[100](0),
	EventName[100]("");	

If CommentaryEnabled Then Begin
	RDate = splatrf.tdate2rdate(Date);
	Condition1 = splatrf.rdate2gdate(RDate, GYear, GMonth, GDay);
	CommentaryCL("GDate: ", NumToStr(GMonth, 0), " / ", NumToStr(GDay, 0), " / ", NumToStr(GYear, 0));
	CommentaryCL("RDate: ", NumToStr(RDate, 0));
	CommentaryCL("Day of Week: ", NumToStr(splatrf.dayofweek(RDate), 0));
	bdw = splatrf.bizdayweek(RDate, FDay, LDay);
	bdws = "";
	If FDay Then bdws = " (first)";
	If LDay Then bdws = " (last)";
	CommentaryCL("Business Day of Week: ", NumToStr(bdw, 0), bdws);
	CommentaryCL("Business Day of Month: ", NumToStr(splatrf.bizdaymonth(RDate), 0));
	Holidays = splatrf.setholidays(GYear, True, Holiday, HolidayName);
	HDays = splatrf.nearestholiday(RDate, Holidays, Holiday, HolidayIndex);
	NearName = HolidayName[HolidayIndex];
	If HDays < 0 Then
		CommentaryCL("It is ", NumToStr(-HDays, 0), " business days before ", NearName, ".");		
	If HDays > 0 Then
		CommentaryCL("It is ", NumToStr(HDays, 0), " business days after ", NearName, ".");		
	Events = splatrf.setevents(GYear, GMonth, Event, EventName);
	EDays = splatrf.nearestevent(RDate, Events, Event, EventIndex);
	NearName = EventName[EventIndex];
	If EDays = 0 Then
		CommentaryCL("Today's Event: ", NearName, ".");		
	If EDays < 0 Then
		CommentaryCL("It is ", NumToStr(-EDays, 0), " business days before ", NearName, ".");		
	If EDays > 0 Then
		CommentaryCL("It is ", NumToStr(EDays, 0), " business days after ", NearName, ".");		
	CommentaryCL("Day of Year: ", NumToStr(splatrf.daynumber(GYear, GMonth, GDay), 0));
	CommentaryCL("Remaining: ", NumToStr(splatrf.daysremain(GYear, GMonth, GDay), 0), NewLine);
	CommentaryCL(NumToStr(GYear, 0), " Holiday Calendar:", NewLine);
	For ih = 0 to Holidays-1 Begin
		Condition1 = splatrf.rdate2gdate(Holiday[ih], HYear, HMonth, HDay);
		CommentaryCL(HolidayName[ih], " ", NumToStr(HMonth, 0), " / ", NumToStr(HDay, 0));
	End;
	CommentaryCL(NewLine, "Events:", NewLine);
	For ie = 0 to Events-1 Begin
		Condition1 = splatrf.rdate2gdate(Event[ie], EYear, EMonth, EDay);
		CommentaryCL(EventName[ie], " ", NumToStr(EMonth, 0), " / ", NumToStr(EDay, 0),
					 " / ", NumToStr(EYear, 0));
	End;
End;

